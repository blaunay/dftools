grammar net.sf.dftools.cdl.Cdl with org.eclipse.xtext.common.Terminals

generate cdl "http://www.sf.net/dftools/cdl/Cdl"

///////////////////////////////////////////////////////////////////////////////
// top-level entity
///////////////////////////////////////////////////////////////////////////////

Module:
	('package' package=QualifiedName ';')?
	(imports += Import)*
	
	( ('component' name=ID component=Component)
	| ('library' name=ID library=Library)
	);
	
Import: 'import' importedNamespace=QualifiedNameWithWildCard ';';

QualifiedName:
	ID ('.' ID)*;

QualifiedNameWithWildCard:
	QualifiedName '.*'?;

	
///////////////////////////////////////////////////////////////////////////////
// Entities
///////////////////////////////////////////////////////////////////////////////


Library:{Library} 
	 '{'
		(types += TypeDecl)*
	'}';

Component:{Component}
	 '(' ')' '{'	
		
		(operator=Operator)?
		
		((interfaces += Interface)
		 |(memories += Memory))*

	'}';

///////////////////////////////////////////////////////////////////////////////
// component classes
///////////////////////////////////////////////////////////////////////////////

Operator:
	'operator' type=[Class|QualifiedName] (tag=Tag)? '{'
	
	'}';

Interface: 
	'interface' type=[Class|QualifiedName] (tag=Tag)? dir=Direction '{'
	
	'}';
	
Memory: 
	'memory' type=[Class|QualifiedName] (tag=Tag)? '{'
	
	'}';

Tag:
(identifiers += ID) ('.' identifiers += ID)*;

Direction: dir=DirSymbol name=ID;

DirSymbol : InDir | OutDir | BiDir;

BiDir : name = '<==>';
OutDir : name = '==>';
InDir : name = '<==';

///////////////////////////////////////////////////////////////////////////////
// library classes
///////////////////////////////////////////////////////////////////////////////

Class: 
	'class' name=ID  '{'
		((attributes+=Attribute)';')*
	'}';

Attribute: type=Type name=ID;

Enum :
	'enum' name=ID '{' 
		(literals += Literal) 
		(',' (literals += Literal))*
	 '}';

///////////////////////////////////////////////////////////////////////////////
// types
///////////////////////////////////////////////////////////////////////////////

TypeDecl: Class | Enum;

Type: TypeBool | TypeInt | TypeLong | TypeString | TypeRef;

TypeRef: type=[TypeDecl];

TypeBool: name='bool';

TypeInt: name='int';

TypeLong: name='long';

TypeString: name='String';

///////////////////////////////////////////////////////////////////////////////
// Litterals
///////////////////////////////////////////////////////////////////////////////

Literal: Integer | Identifier;

Integer: index=INT;

Identifier: name=ID;
